buildscript{
  repositories{
    jcenter()
  }
  dependencies{
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
  }
}

group 'com.github.shorn'
version '0.2.1'

String scalaVersion = '2.11'
String subVersion = '8'
String scalaJSVersion = '0.6.13'

apply plugin: 'scala'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8

repositories{
  mavenCentral()
}

dependencies{
  compile gradleApi()
  compile 'org.codehaus.groovy:groovy-all:2.4.4'
  compile 'org.scala-lang:scala-compiler:' + scalaVersion + '.' + subVersion
  compile 'org.scala-lang:scala-library:' + scalaVersion + '.' + subVersion
  compile group: 'org.scala-js', name: 'scalajs-sbt-test-adapter_' + scalaVersion, version: scalaJSVersion
  compile group: 'org.scala-js', name: 'scalajs-js-envs_' + scalaVersion, version: scalaJSVersion
  compile group: 'org.scala-js', name: 'scalajs-tools_' + scalaVersion, version: scalaJSVersion
  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'com.google.guava', name: 'guava', version: '19.0'
}

task javadocJar(type: Jar){
  classifier = 'javadoc'
  from javadoc
}

task groovydocJar(type: Jar){
  classifier = 'groovydoc'
  from groovydoc
}

task scaladocJar(type: Jar){
  classifier = 'scaladoc'
  from scaladoc
}

task sourcesJar(type: Jar){
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts{
  archives javadocJar, groovydocJar, scaladocJar, sourcesJar
}

publishing {
  publications {
    'scalajs-plugin'(MavenPublication) {
      from components.java
      groupId project.group
      artifactId project.name // from settings.gradle
      version project.version
    }
  }
}

bintray {
  user = System.getProperty("scalajsPluginBintrayPublishUser")
  key = System.getProperty("scalajsPluginBintrayPublishKey")
  dryRun = Boolean.getBoolean("scalajsPluginBintrayDryRun")
  publish = true
  pkg {
    repo = 'scalajs-plugin'
    name = project.name // based off project directory name
    desc = 'ScalaJs Gradle plugin'
    // BSD because that's what the maven central repo says,
    // couldn't find anything in the source repo
    licenses = ['BSD']
    vcsUrl = 'https://github.com/shorn/scalajs-gradle.git'

    publications = ['scalajs-plugin']

    //noinspection GroovyAssignabilityCheck
    version {
      name = project.version
      desc = "scalajs plugin $project.version"
      publicDownloadNumbers = true
      released  = new Date()
    }
  }
}

tasks.bintrayUpload.doFirst({
  println "uploading plugin version ${project.version} to bintray"
})

